plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    def localProperties = new Properties()
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))

    compileSdk 33

    defaultConfig {
        applicationId "org.jorgetargz.movies"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "API_KEY", "\"" + localProperties['API_KEY'] + "\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'org.jorgetargz.movies'
}

dependencies {

    // Android libraries
    implementation 'androidx.core:core-ktx:1.9.0'

    // AppCompat
    implementation 'androidx.appcompat:appcompat:1.6.0'

    // Material Design
    implementation 'com.google.android.material:material:1.7.0'

    // ConstraintLayout
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Lifecycle components
    def lifecycle_version = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    // by viewModels delegate
    implementation "androidx.fragment:fragment-ktx:1.5.5"

    // Navigation components
    def navigationVersion = "2.5.3"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    // Retrofit and Gson
    def retrofitVersion = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"

    // OkHttp3 logging interceptor
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.1"

    // Dagger Hilt for dependency injection
    def daggerVersion = "2.42"
    implementation "com.google.dagger:hilt-android:$daggerVersion"
    kapt "com.google.dagger:hilt-compiler:$daggerVersion"

    // Timber for logging
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Room Database for local persistence
    def room_version = '2.5.0'
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Coil for image loading
    implementation 'io.coil-kt:coil:2.2.2'
}

kapt {
    correctErrorTypes true
}
